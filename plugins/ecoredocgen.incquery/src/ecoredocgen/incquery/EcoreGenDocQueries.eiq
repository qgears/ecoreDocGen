package ecoredocgen.incquery

import "http://www.eclipse.org/emf/2002/Ecore"

pattern eCoreDocumentation(host:ENamedElement, ann:EAnnotation, doc:EString) 
{
	find eCoreDocAnnotation(host,ann);
	EAnnotation.details(ann,mapEntry);
	EStringToStringMapEntry.key(mapEntry,"documentation");
	EStringToStringMapEntry.value(mapEntry,doc);
}

/**
 * Lists ENamedElement - documentation EAnnotation pairs
 */
pattern eCoreDocAnnotation(host:ENamedElement, ann:EAnnotation){
	ENamedElement.eAnnotations(host,ann);
	EAnnotation.source(ann,"http://www.eclipse.org/emf/2002/GenModel");
}
@Constraint(
	location = host,
	targetEditorId = "org.eclipse.emf.ecore.presentation.EcoreEditorID",
	message = "$host$ is missing the EcoreGen Documentation",
	severity = "warning"
)
@QueryExplorer(display = true, message = "$host$ is missing the documentation completely")
pattern missingEcoreDocumentation(host:ENamedElement)
{
	neg find eCoreDocumentation(host, _ann, _doc);
}

pattern missingEcoreDocumentation_EClass(host:EClass)
{
	find missingEcoreDocumentation(host);
}

pattern missingEcoreGenDocumentation_EPackage(host:EPackage)
{
	find missingEcoreDocumentation(host);
}

@Constraint(
	location = host,
	targetEditorId = "org.eclipse.emf.ecore.presentation.EcoreEditorID",
	message = "$host$ has an empty EcoreGen Documentation",
	severity = "warning"
)
@QueryExplorer(display = true, message = "$host$ has an empty EcoreGen Documentation")
pattern zeroLengthEcoreGenDocumentation(host:ENamedElement)
{
	find eCoreDocumentation(host,_ann, doc);
	check(doc.length == 0);
}

@Constraint(
	location = host,
	targetEditorId = "org.eclipse.emf.ecore.presentation.EcoreEditorID",
	severity = "warning",
	message = "$host$ has a suspiciously short documentation"
)
@QueryExplorer(display = true, message = "$host$ has a suspiciously short documentation")
pattern tooShortEcoreGenDocumentation(host:ENamedElement)
{
	find eCoreDocumentation(host,_ann, doc);
	check(doc.length>0 && doc.length<50);
}

